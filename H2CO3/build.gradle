plugins {
    id 'com.android.application'
}

// 读取前面配置的keystore.properties
def keyProps = new Properties()
def keyPropsFile = rootProject.file('keystore/keystore.properties')
if (keyPropsFile.exists()) {
    keyProps.load(new FileInputStream(keyPropsFile))
}

// 读取version.properties
def versionProps = new Properties()
def versionPropsFile = rootProject.file('version.properties')
if (versionPropsFile.exists()) {
    versionProps.load(new FileInputStream(versionPropsFile))
}

android {
    namespace 'org.koishi.launcher.h2co3'
    compileSdk 34

    defaultConfig {
        applicationId "org.koishi.launcher.h2co3"
        minSdk 28
        targetSdk 34
        defaultConfig {
            versionCode versionProps['versionCode'].toInteger()
            versionName versionProps['versionName']
        }
    }

    signingConfigs {
        release {
            keyAlias keyProps['keyAlias']
            keyPassword keyProps['keyPassword']
            storeFile keyProps['storeFile'] ? file(keyProps['storeFile']) : null
            storePassword keyProps['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    ndkVersion '25.2.9519653'
}

tasks.register('upgradeVersion') {
    group 'help'
    description '构建新版本'
    doLast {
        println("---自动升级版本号---\n")
        versionProps = new Properties()
        versionPropsFile = rootProject.file('version.properties')
        if (versionPropsFile.exists()) {
            versionProps.load(new FileInputStream(versionPropsFile))
        }
        String oldVersionCode = versionProps['versionCode']
        String oldVersionName = versionProps['versionName']
        if (oldVersionCode == null || oldVersionName == null ||
                oldVersionCode.isEmpty() || oldVersionName.isEmpty()) {
            println("error:版本号不能为空")
            return
        }
        versionProps['versionCode'] = String.valueOf(versionProps['versionCode'].toInteger() + 1)
        String str = versionProps['versionName'].toString()
        versionProps['versionName'] = str.substring(0, str.lastIndexOf('.') + 1) +
                (str.substring(str.lastIndexOf('.') + 1).toInteger() + 1)
        String tip =
                "版本号从$oldVersionName($oldVersionCode)升级到${versionProps['versionName']}(${versionProps['versionCode']})"
        println(tip)

        def writer = new FileWriter(versionPropsFile)
        versionProps.store(writer, null)
        writer.flush()
        writer.close()
        def tag = "v${versionProps['versionName']}"
        cmdExecute("git pull")
        cmdExecute("git add version.properties")
        cmdExecute("git commit -a -m \"版本号升级为：$tag\"")
        cmdExecute("git push origin")
        cmdExecute("git tag $tag")
        cmdExecute("git push origin $tag")
    }
}

void cmdExecute(String cmd) {
    println "\n执行$cmd"
    println cmd.execute().text
}


configurations.configureEach {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':H2CO3Core')
    implementation project(path: ':H2CO3Resources')
    implementation project(path: ':H2CO3Boat')

    // Google
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.google.dagger:hilt-android:2.48'

    // AndroidX
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.6.0'
    implementation 'androidx.navigation:navigation-ui:2.6.0'
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.webkit:webkit:1.7.0-alpha03'

    // Activity Permissions
    implementation 'com.github.getActivity:XXPermissions:16.8'

    // Custom Activity On Crash
    implementation 'cat.ereza:customactivityoncrash:2.4.0'

    // Rikka
    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.6.1'
    implementation 'dev.rikka.rikkax.compatibility:compatibility:2.0.0'
    implementation 'dev.rikka.rikkax.material:material:2.7.0'
    implementation 'dev.rikka.rikkax.material:material-preference:2.0.0'
    implementation 'dev.rikka.rikkax.html:html-ktx:1.1.2'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-adapter:1.3.0'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-ktx:1.3.2'
    implementation 'dev.rikka.rikkax.insets:insets:1.3.0'
    implementation 'dev.rikka.rikkax.layoutinflater:layoutinflater:1.3.0'
    implementation 'dev.rikka.rikkax.widget:borderview:1.1.0'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-resource-livedata:1.0.1'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-shared-viewmodel:1.0.1'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-viewmodel-lazy:2.0.0'

    // Volley
    implementation 'com.android.volley:volley:1.2.1'

    // Fastjson
    implementation 'com.alibaba:fastjson:2.0.40'

    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.11'

    // HoloColorPicker
    implementation 'com.larswerkman:HoloColorPicker:1.5'

    // GreenRobot EventBus
    implementation "org.greenrobot:eventbus:3.3.1"

    // Billing Client
    implementation 'com.android.billingclient:billing:5.1.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.15.1'
}